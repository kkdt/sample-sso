/**
 * Copyright (C) 2018 thinh ho
 * This file is part of 'sample-sso' which is released under the MIT license.
 * See LICENSE at the project root directory.
 */

buildscript {
   repositories { 
      mavenCentral()
      maven {
         url 'https://github.com/kkdt/mavenrepo/raw/master'
      }
   }
   dependencies {
      classpath "kkdt.gradle.eclipseone:eclipseone:0.5"
   }
}

description = 'Sample Single-Sign-On (master build)'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'kkdt.gradle.eclipseone'

defaultTasks 'clean', 'build'

eclipse {
   classpath {
      downloadJavadoc = true
      downloadSources = true
   }
   
   project {
      resourceFilter {
         appliesTo = 'FOLDERS'
         type = 'EXCLUDE_ALL'
         matcher {
           id = 'org.eclipse.ui.ide.multiFilter'
           arguments = '1.0-name-matches-false-false-build'
         }
       }
   }
}

allprojects {
    version = '0.1'
    group = 'kkdt.sample'
}

subprojects {
   apply plugin: 'java'
   apply plugin: 'maven'
   
   dependencies {
       compile 'log4j:log4j:1.2.17'
       testCompile 'junit:junit:4.12'
   }
   
   afterEvaluate { p ->
       if(p.plugins.hasPlugin('application')) {
           p.distTar {
               destinationDir = rootProject.buildDir
           }
           
           p.distZip {
               destinationDir = rootProject.buildDir
           }
       }
       
       p.processResources {
           from(rootProject.projectDir) {
               include 'log4j.xml'
           }
       }
   }
}

allprojects {
   version = rootProject.version
   
   repositories {
      mavenCentral()
      mavenLocal()
   }
}

// the root project does not have any build artifacts
build.doLast {
   delete buildDir
}

